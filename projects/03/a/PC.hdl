// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
		Or(a=inc, b=load, out=outOr0);
		Or(a=outOr0, b=reset, out=outOr1);

		Inc16(in=outReg, out=outInc);
		
		Mux16(a=outInc, b=in, sel=load, out=outMux0);
		Mux16(a=outMux0, b=false, sel=reset, out=outMux1);

    Register(in=outMux1, load=outOr1, out=outReg, out=out);
}
